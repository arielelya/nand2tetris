// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the output bits:
 * zr = (out == 0, 1, 0)
 * ng = (out < 0,  1, 0)
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not
CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // (out == 0, 1, 0)
        ng;      // (out < 0,  1, 0)

    PARTS:
    //zx:
    Mux16(a=x, b=false, sel=zx, out=zxOut);

    //nx:
    Not16(in=zxOut, out=NotzxOut);
    Mux16(a=zxOut, b=NotzxOut, sel=nx, out=preX);

    //zy:
    Mux16(a=y, b=false, sel=zy ,out=zyOut);

    //ny:
    Not16(in=zyOut, out=NotzyOut);
    Mux16(a=zyOut, b=NotzyOut, sel=ny, out=preY);

    //f:
    Add16(a=preX, b=preY, out=aPlusb);
    And16(a=preX, b=preY, out=aAndb);
    Mux16(a=aAndb, b=aPlusb, sel=f, out=plusOrAdd);

    //no:
    Not16(in=plusOrAdd, out=notPlusOrAdd);
    Mux16(a=plusOrAdd, b=notPlusOrAdd, sel=no, out[0..7]=firstHalf, out[8..15]=secondHalf, out[15]=first);
    Mux16(a=plusOrAdd, b=notPlusOrAdd, sel=no, out=out);

    //zr:
    Or8Way(in= firstHalf, out=temp1);
    Or8Way(in= secondHalf, out=temp2);
    Or(a=temp1, b=temp2, out=zrOp);
    Not(in=zrOp, out=zr);


    //ng:
    Or(a=first, b=false, out=ng);
    
}